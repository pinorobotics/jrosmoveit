plugins {
    id 'java'
}

apply plugin : 'eclipse'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.lambdaprime:jrosclient:3.0'
    implementation 'io.github.pinorobotics:jrosactionlib:1.0'
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // transitively export following dependency to tests project
    apiElements 'io.github.lambdaprime:jrosclient:3.0'
    apiElements 'io.github.pinorobotics:jrosactionlib:1.0'    
    apiElements fileTree(include: ['*.jar'], dir: 'libs')
}

sourceCompatibility = '11'
targetCompatibility = '11'

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}"
    }
}

java {
    modularity.inferModulePath = true
}

sourceSets {
    main {
        java {
            srcDirs += ["src/main/gen"]
        }
    }
}

javadoc {
    options.noTimestamp = true;
    options.noQualifiers = ["all"];
    options.noTree = true;
    options.noIndex = true;
    options.noNavBar = true;
    options.addStringOption('Xdoclint:none', '-quiet')
}

def packageDir = "$buildDir/package/${projectName}"

ext.artifacts = fileTree(include: ['*'], dir: "$packageDir/libs")

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

task copyLibs(type: Copy) {
    from configurations.default
    from jar.archiveFile
    into "${packageDir}/libs"
}

task createPackage(type: Copy, dependsOn: [copyLibs]) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    into packageDir
}

task cleanRelease(type: Delete) {
    delete "$packageDir"
    delete "$projectDir/release"
}

createRelease.dependsOn createPackage, javadoc
assemble.dependsOn createRelease
clean.dependsOn cleanRelease

apply plugin: 'maven-publish'

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'jrosmoveit'
            group = "io.github.pinorobotics"
            version = "${version}"
            artifact jar.archiveFile
        }
    }
}
